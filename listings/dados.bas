   5 RUN 10
  10 REM  PRUEBE SU SUERTE 
  20 BORDER 3: PAPER 6: INK 0: CLS 
  30 POKE 23609,55: POKE 23658,8
  40 GO SUB 5000
  50 GO SUB 4000
  60 GO SUB 3000
  70 GO SUB 2000
1000 REM  JUEGO 
1010 LET JX=50: PRINT AT 5,26;JX
1020 INPUT "HAGA SU APUESTA: ";A
1040 LET JX=JX-A: PRINT AT 5,26;"  ": PRINT AT 5,26;JX;" ": PRINT AT 19,1;"APUESTA ";A;" PTS AL"
1050 INPUT "A QUE NUMERO (1-6):";B
1060 IF B<1 OR B>6 THEN GO TO 1050
1070 PRINT AT 19,23;B: PAUSE 10
1080 GO SUB 3000
1090 GO SUB 2000
1100 IF Q=B THEN LET Z=Z+1: PRINT AT 9,25;"*"
1110 IF R=B THEN LET Z=Z+1: PRINT AT 10,25;"*"
1120 IF S=B THEN LET Z=Z+1: PRINT AT 11,25;"*"
1130 PRINT AT 7,27;"  ";AT 7,27;Z
1140 IF Z=0 THEN PRINT AT 19,1;"PIERDE                          ": GO TO 1500
1150 IF Z=1 THEN PRINT AT 19,1;"GANA                            ": LET JX=JX+A: PRINT AT 5,26;"   ": PRINT AT 5,26;JX: GO TO 1500
1160 IF Z=2 THEN PRINT AT 19,1;"GANA                            ": LET JX=JX+(A*2): PRINT AT 5,26;"   ": PRINT AT 5,26;JX: GO TO 1500
1170 IF Z=3 THEN PRINT AT 19,1;"GANA                            ": LET JX=JX+(A*3): PRINT AT 5,26;"  ": PRINT AT 5,26;JX: GO TO 1500
1500 IF JX=0 THEN PRINT AT 19,1;"NO  LE QUEDA DINERO": PAUSE 50: GO TO 1900
1510 FOR N=1 TO 3: BEEP .25,25: BEEP .25,35: NEXT N
1520 PRINT #0;AT 0,0; FLASH 1;"OTRA JUGADA ]NO? "
1525 PAUSE 0
1530 IF INKEY$="N" THEN PRINT #0;AT 0,0; FLASH 1;"ERES UN CAGAO": GO TO 1900
1540 IF INKEY$="S" THEN GO TO 1550
1545 GO TO 1525
1550 LET Z=0: PRINT AT 7,27;Z;AT 19,1;"                                "
1560 PRINT AT 7,10; PAPER 4;"Ⓖ";AT 9,12;"Ⓖ";AT 11,11;"Ⓖ": PRINT AT 9,23;"   ";AT 10,23;"   ";AT 11,23;"   ": GO TO 1020
1900 PAUSE 0: CLS : PRINT AT 10,5;"GRACIAS POR JUGAR": FOR X=1 TO 26: BEEP .1,X: NEXT X
1999 LOAD "M:": LOAD "MENU"
2000 REM  DADOS A NUMEROS 
2010 LET Q=L: LET R=M: LET S=N
2020 PRINT AT 9,23;Q;AT 10,23;R;AT 11,23;S
2998 RETURN 
2999 STOP 
3000 REM  LANZAMIENTO DE DADOS 
3010 LET A$="ⒶⒷⒸⒹⒺⒻ"
3020 RANDOMIZE 
3030 LET L=INT (RND*6)+1
3040 LET M=INT (RND*6)+1
3050 LET N=INT (RND*6)+1
3060 LET X=7: FOR Y=0 TO 9
3070 PRINT AT X,Y;A$(L): PAUSE 2: PRINT AT X,Y; PAPER 4;"Ⓖ": NEXT Y: PRINT AT 7,10;A$(L): BEEP .25,30
3080 LET X=9: FOR Y=0 TO 11
3090 PRINT AT X,Y;A$(M): PAUSE 2: PRINT AT X,Y; PAPER 4;"Ⓖ": NEXT Y: PRINT AT 9,12;A$(M): BEEP .25,25
3100 LET X=11: FOR Y=0 TO 10
3110 PRINT AT X,Y;A$(N): PAUSE 2: PRINT AT X,Y; PAPER 4;"Ⓖ": NEXT Y: PRINT AT 11,11;A$(N): BEEP .25,30
3998 RETURN 
3999 STOP 
4000 REM  DISE\O DE LA PANTALLA 
4010 PRINT INVERSE 1; INK 1;AT 1,6;"* PRUEBE SU SUERTE *"
4020 PRINT AT 3,1;"APUESTE A UN NUMERO (1-6)";AT 5,16;"TIENE PTS:"
4030 FOR X=6 TO 12: FOR Y=0 TO 13: PRINT AT X,Y; PAPER 4;"Ⓖ": PRINT AT X,14;"█": NEXT Y: NEXT X
4040 FOR Y=0 TO 14: PRINT AT 5,Y;"█";AT 13,Y;"█": NEXT Y
4050 PRINT AT 15,1;"1 ACIERTO: DINERO ATRAS";AT 16,1;"2 ACIERTOS: GANA EL DOBLE";AT 17,1;"3 ACIERTOS: GANA EL TRIPLE"
4060 PRINT AT 7,18;"MARCADOR:";Z: LET Z=0: PRINT AT 9,16;"DADO 1:";AT 10,16;"DADO 2:";AT 11,16;"DADO 3:"
4998 RETURN 
4999 STOP 
5000 REM  CARACTERES GRAFICOS 
5010 FOR X=0 TO 6: FOR Y=0 TO 7: READ Z: POKE USR CHR$ (144+X)+Y,Z: NEXT Y: NEXT X
5020 DATA 255,255,255,247,247,255,255,255,255,253,253,255,255,223,223,255,255,253,253,247,247,223,223
5030 DATA 255,255,221,221,255,255,221,221,255,255,221,221,247,247,221,221,255,255,213,213,255,255,213,213,255,85,0,85,0,85,0,85,0
5998 RETURN 
5999 STOP 
9999 SAVE "M:DADOS" LINE 0: PRINT AT 1,0: LOAD ""
